// ∗∗∗∗∗∗∗∗∗ Important – Do not use in production, for demonstration purposes only – please review the legal notices before continuing ∗∗∗∗∗ */

//What is the average temperature every 1 min?
temp
| where DateTimeUTC > ago(2d)
| where DeviceID == 'Device1'
| summarize avg(Temp) by bin(DateTimeUTC,1m)
| render timechart 

//What will be the temperature for next 15 Minutes?
temp
| where DateTimeUTC > ago(2d)
| where DeviceID == 'Device1'
| make-series AvgTemp=avg(Temp) default=real(null) on DateTimeUTC from ago(2d) to now()+15m step 1m   
| extend NoGapsTemp=series_fill_linear(AvgTemp)
| project DateTimeUTC, NoGapsTemp
| extend forecast = series_decompose_forecast(NoGapsTemp, 15)
| render timechart with(title='Forecasting the next 15min by Time Series Decmposition')

temp 
| where DateTimeUTC > ago(12h)
| where DeviceID == 'Device1'
| make-series AvgTemp=avg(Temp) default=real(null) on DateTimeUTC from ago(12h) to now() step 1m   
| extend NoGapsTemp=series_fill_linear(AvgTemp)
| project DateTimeUTC, NoGapsTemp
| extend anomalies = series_decompose_anomalies(NoGapsTemp,1) 
| render anomalychart with(anomalycolumns=anomalies)
